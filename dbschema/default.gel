module default {

    scalar type BoardType extending enum<Stellar, Galactic, Cosmic>;

    global current_user := (
        assert_single((
            select User
            filter global ext::auth::ClientTokenIdentity in .identity
        ))
    );

    type User {
        multi identity: ext::auth::Identity {
            constraint exclusive;
        }
        email: str {
            constraint exclusive;
        }
        username: str {
            constraint exclusive;
        }
        avatar: Avatar;
    }

    abstract type PixelGraphic {
        required binary: bytes;
        required size: BoardType;
        required creator: User;
        
        required created_at: datetime { default := datetime_of_statement() };
        required updated_at: datetime { default := datetime_of_statement() };
    }

    type StaticImage extending PixelGraphic {}

    type Avatar extending StaticImage {
        constraint expression on (.size = BoardType.Stellar) {
            errmessage := "Avatars must use the Stellar (16x16) board type";
        }
    }

    type PixelAnimation extending PixelGraphic {
        required frames: int16;
        required frame_delay_ms: int16 {
            default := 100;
            constraint min_value(10);
            constraint max_value(2000);
        }
    }

    type Board {
        required owner: User;
        required boardType: BoardType;
        name: str;
        secret_key_hash: str;
        secret_updated_at: datetime {
            rewrite update using (
                if exists(.secret_key_hash ?? <optional str>{}) then
                    datetime_of_statement()
                else
                    .secret_updated_at
            )
        }
        last_connected_at: datetime;
    }

    type Message {
        required graphic: PixelGraphic;
        required sender: User;
        recipient: User;
        
        is_read: bool { default := false };
        sent_at: datetime { default := datetime_of_statement() };
    }

    type FriendRequest {
        required sender: User;
        required recipient: User;
        required created_at: datetime {
            rewrite insert using (datetime_of_statement());
        }
        constraint exclusive on ((.sender, .recipient));
        constraint expression on (.sender != .recipient) {
            errmessage := "Cannot send friend request to yourself";
        }
    }

    type Friend {
        required user1: User;
        required user2: User;
        required created_at: datetime {
            rewrite insert using (datetime_of_statement());
        }
        constraint exclusive on ((.user1, .user2));
        constraint expression on (.user1 != .user2) {
            errmessage := "Cannot be friends with yourself";
        }
    }
}
